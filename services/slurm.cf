#
## slurm.cf  @SURFsara
#
bundle agent slurm()
{
    vars:
        any::
            "config_file"           string => "$(config_dir)/slurm.conf";
            "cgroup_config_file"    string => "$(config_dir)/cgroup.conf";
            "cgroup_allowed_file"   string => "$(config_dir)/cgroup_allowed_devices_file.conf";
            "gres_config_file"      string => "$(config_dir)/gres.conf";
            "nodes_config_file"     string => "$(config_dir)/nodes.conf";
            "plugstack_config_file" string => "$(config_dir)/plugstack.conf";
            "slurmdbd_file"         string => "$(config_dir)/slurmdbd.conf";
            "topology_config_file"  string => "$(config_dir)/topology.conf";

            "scontrol_cmd"          string => "/usr/bin/scontrol";

            "gid"                   string => "555";
            "group"                 string => "slurm";
            "mode"                  string => "0644";
            "owner"                 string => "slurm";
            "uid"                   string => "555";

            "template_2_destination" data => parsejson('{
                    "cgroup.mustache": "$(cgroup_config_file)",
                    "cgroup_allowed_devices_file.mustache": "$(cgroup_allowed_file)",
                    "gres.mustache": "$(gres_config_file)",
                    "nodes.mustache": "$(nodes_config_file)",
                    "plugstack.conf.mustache": "$(plugstack_config_file)",
                    "slurm.mustache": "$(config_file)",
                    "slurmdbd.mustache": "$(slurmdbd_file)",
                    "topology.mustache": "$(topology_config_file)",
            }');

        any::
            "dir" string => "$(sara_data.slurm[dir])";
            "config_dir" string => "$(sara_data.slurm[config_dir])";
            "current_version" string => "$(sara_data.slurm[current_version])";
            "log_dir" string => "$(sara_data.slurm[log_dir])";
            "opt_dir" string => "$(sara_data.slurm[opt_dir])";
            "plugin_dir" string => "$(sara_data.slurm[plugin_dir])";
            "plugstack_dir" string => "$(sara_data.slurm[plugstack_dir])";
            "scripts_dir" string => "$(sara_data.slurm[scripts_dir])";
            "software_dir" string => "$(sara_data.slurm[software_dir])";
            "spool_dir" string => "$(sara_data.slurm[spool_dir])";
            "tarball_dir" string => "$(sara_data.slurm[tarball_dir])";

        systemd::
            "client_service_name" string => "slurmd";
            "controller_service_name" string => "slurmctld";
            "database_service_name" string => "slurmdbd";

            "restart_client"     string => "$(paths.path[systemctl]) restart $(client_service_name)";
            "restart_controller" string => "$(paths.path[systemctl]) restart $(controller_service_name)";
            "restart_database"   string => "$(paths.path[systemctl]) restart $(database_service_name)";

            "reload_client"     string => "$(paths.path[systemctl]) reload $(client_service_name)";
            "reload_controller" string => "$(scontrol_cmd) reconfigure";
            "reload_database"   string => "$(paths.path[systemctl]) reload $(database_service_name)";
}

bundle agent slurm_autorun()
{
    meta:
        "tags" slist => { "autorun", "template_slurm" };

    classes:
        "spank_templates_set"  expression => isvariable("sara_data.slurm[spank_templates]"),
            comment => "Must we generate spank plugin setup";

        "disable_services_set" expression => isvariable("sara_data.slurm[disable_services]");

    methods:
        any::
            "" usebundle => slurm();
            "" usebundle => slurm_install();
            "" usebundle => slurm_config();
            "" usebundle => slurm_daemon_check();

        spank_templates_set::
           "" usebundle => slurm_spank_templates();

        SLURM_BACKUP::
            "" usebundle => slurm_db_backup(),
                if => or(
                    "$(sara_data.slurm[backup_schedule])",
                    "SLURM_BACKUP_SCHEDULE"
                    ),
                action => if_elapsed(50);

        SLURM_LOGROTATE::
            "" usebundle => slurm_logrotate(),
                comment => "slurm service bundle handles logrotate",
                if => or(
                    "$(sara_data.slurm[logrotate_schedule])",
                    "SLURM_LOGROTATE_SCHEDULE"
                    ),
                action => if_elapsed(50);

        systemd.disable_services_set::
            "" usebundle => systemd_disable_service("$(sara_data.slurm[disable_services])");

    reports:
        systemd.disable_services_set::
            "$(this.bundle): systemd services disable for: $(sara_data.slurm[disable_services])"
                if => "DEBUG|DEBUG_slurm";
}

bundle agent slurm_install()
{
    methods:
        !SLURM_TARBALL::
            "" usebundle => slurm_install_pkg();

        SLURM_TARBALL::
            "" usebundle => slurm_install_tarball();

        SLURM_SYSTEMD_SERVICES|SLURM_TARBALL::
            "" usebundle => slurm_generate_systemd_services();
}

bundle agent slurm_install_pkg()
{
    vars:
        any::
            "version" string => ifelse( isvariable("sara_data.slurm[version]"), "$(sara_data.slurm[version])", "");
            "pyslurm_version" string => ifelse( isvariable("sara_data.slurm[pyslurm_version]"), "$(sara_data.slurm[pyslurm_version])", "");
            "packages" data => parsejson('{}');

        SLURM_SERVER:: "packages" data => mergedata( "packages", "sara_data.slurm[packages_server]"),
                comment => 'List of slurm packages';

        SLURM_CLIENT::
            "packages" data => mergedata( "packages", "sara_data.slurm[packages_client]"),
                comment => 'List of slurm packages';

        SLURM_SUBMIT::
            "packages" data => mergedata( "packages", "sara_data.slurm[packages_submit]"),
                comment => 'List of slurm packages';

    methods:
        any::
            "" usebundle =>  sara_service_packages("slurm", "@(slurm_install_pkg.packages)"),
                classes => results("bundle", "slurm_packages");

        SLURM_SLURMD_DISABLE::
            "" usebundle => systemd_stop_service("slurm"),
                if => "slurm_packages_repaired";
}

bundle agent slurm_install_tarball()
{
    vars:
        any::
            "header" string => "## Maintained by CFengine$(const.n)#";
            "template" string => "$(header)
$(slurm.software_dir)/current/lib
$(slurm.software_dir)/current/lib/slurm
";

    classes:
        any::
            "tarballs_set" expression => isvariable("sara_data.slurm[install_tarballs]"),
                comment => "Must we copy tarballs for slurm";

            "slurm_grp" expression => groupexists("$(slurm.group)");

    files:
        any::
            "$(slurm.software_dir)/current"
                link_from => ln_s("$(slurm.software_dir)/$(slurm.current_version)"),
                move_obstructions => "true",
                classes => results("bundle", "current_version");

            "/etc/ld.so.conf.d/slurm.conf"
                create => "true",
                edit_defaults => no_backup,
                edit_template_string => "$(template)",
                template_method => "inline_mustache",
                classes => results("bundle", "ld_config_slurm");

            "$(slurm.log_dir)/."
                create => "true",
                perms => mog("0700", "root", "root");

            "$(slurm.software_dir)/."
                perms => mog("0755", "root", "root"),
                create => "true";


        slurm_tarball_repaired|current_version_repaired|SLURM_FORCE_LINKS::
             "/usr/bin"
                comment => "slurm client program link them in /usr/bin",
                link_from => linkchildren("$(slurm.software_dir)/current/bin");

             "/usr/sbin"
                comment => "slurm client program link them in /usr/sbin",
                link_from => linkchildren("$(slurm.software_dir)/current/sbin");

             "$(pam.lib_dir)/pam_slurm_adopt.so"
                copy_from   => local_dcp("$(slurm.software_dir)/current/pam/pam_slurm_adopt.so"),
                perms => mog("0755", "root", "root");

    methods:
        any::
            "" usebundle => sara_service_install_tarballs("slurm"),
                classes => results("bundle", "slurm_tarball");

    users:
        slurm_grp::
            "slurm"
                policy => "present",
                home_dir => "/nonexistent",
                group_primary => "$(slurm.group)",
                uid => "$(slurm.uid)",
                shell => "/bin/false";

    commands:
        any::
            "/sbin/ldconfig"
                if => "ld_config_slurm_repaired";

            "$(paths.path[groupadd]) --gid $(slurm.gid) $(slurm.group)"
                if => "!slurm_grp";

        SLURM_CLIENT.current_version_repaired::
            "$(slurm.restart_client)";
}

bundle agent slurm_daemon_check()
{
    processes:
        SLURM_CLIENT::
             "$(slurm.client_service_name)"
                  comment        => "Check if slurmd is running",
                  process_count  => check_range("$(slurm.client_service_name)", "1", "1"),
                  process_select => sara_select_parent_process("1");

        SLURM_SERVER::
             "$(slurm.controller_service_name)"
                  comment        => "Check if slurmctld is running",
                  process_count  => check_range("$(slurm.controller_service_name)", "1", "1"),
                  process_select => sara_select_parent_process("1");

             "$(slurm.database_service_name)"
                  comment        => "Check if slurmdbd is running",
                  process_count  => check_range("$(slurm.database_service_name)", "1", "1"),
                  process_select => sara_select_parent_process("1");

    commands:
        any::
            "$(slurm.restart_client)"
                if => and(canonify("$(slurm.client_service_name)_out_of_range"));

            "$(slurm.restart_controller)"
                if => and(canonify("$(slurm.controller_service_name)_out_of_range"));

            "$(slurm.restart_database)"
                if => and(canonify("$(slurm.database_service_name)_out_of_range"));
}


bundle agent slurm_config()
{
    vars:
        any::
            "cgroup_file_class"    string => canonify("sara$(slurm.cgroup_config_file)");
            "gres_file_class"    string => canonify("sara$(slurm.gres_config_file)");
            "nodes_file_class"    string => canonify("sara$(slurm.nodes_config_file)");
            "plugstack_file_class"  string => canonify("sara$(slurm.plugstack_config_file)");
            "slurmd_file_class"    string => canonify("sara$(slurm.config_file)");
            "topology_file_class"    string => canonify("sara$(slurm.topology_config_file)");

            "slurm_mog_list" slist => {
                "$(slurm.config_file)",
                "$(slurm.cgroup_config_file)",
                "$(slurm.cgroup_allowed_file)",
                "$(slurm.nodes_config_file)",
                "$(slurm.plugstack_config_file)",
                "$(slurm.topology_config_file)",
            },
                comment => "These files must be owned by slurm user";

    classes:
        any::
            "slurm_reload" or => {
                "$(cgroup_file_class)",
                "$(gres_file_class)",
                "$(plugstack_file_class)",
                "$(slurmd_file_class)",
                "$(topology_file_class)",
            };

            "slurm_restart" or => {
                "$(nodes_file_class)",
            };

            "slurm_copy_dirs_set" expression => isvariable("sara_data.slurm[copy_dirs]"),
                comment => "Must we copy dirs for slurm";
            "slurm_copy_files_set" expression => isvariable("sara_data.slurm[copy_files]"),
                comment => "Must we copy dirs for slurm";

    methods:
        any::
            "" usebundle => sara_mustache_autorun("slurm");

        slurm_copy_dirs_set::
            "" usebundle => sara_service_copy_dirs("slurm");

        slurm_copy_files_set::
            "" usebundle => sara_service_copy_files("slurm");

        slurm_restart::
            "" usebundle => slurm_service_restart();
        slurm_reload.!slurm_restart::
            "" usebundle => slurm_service_reload();

    files:
        any::
            "$(slurm_mog_list)"
                perms => mog("$(slurm.mode)", "$(slurm.owner)", "$(slurm.group)");

            "$(slurm.config_dir)"
                perms => mog("$(slurm.mode)", "$(slurm.owner)", "$(slurm.group)");

            "$(slurm.spool_dir)/."
                perms => mog("${slurm.mode}", "$(slurm.owner)", "$(slurm.group)"),
                create => "true";
}

bundle agent slurm_service_restart()
{
    commands:
        SLURM_CLIENT::
            "$(slurm.restart_client)";

        SLURM_SERVER::
            "$(slurm.restart_controller)";

    reports:
        any::
            "$(this.bundle): Restart slurm service a config file has been changed that require this";
}

bundle agent slurm_service_reload()
{
    commands:
        SLURM_CLIENT::
            "$(slurm.reload_client)";

        SLURM_SERVER::
            "$(slurm.reload_controller)";

    reports:
        any::
            "$(this.bundle): Reload slurm service a config file has been changed that require this";
}

bundle agent slurm_db_backup()
{
    vars:
        "cmd" string => format("%s --host=%s --user=%s --password=%s %s",
            $(sara_data.slurm[backup_config][cmd]),
            $(sara_data.slurm[backup_config][host]),
            $(sara_data.slurm[backup_config][user]),
            $(sara_data.slurm[backup_config][password]),
            $(sara_data.slurm[backup_config][options])
            );

    files:
        "$(sara_data.slurm[backup_dir])/."
            perms => mog("700", "root", "root"),
            create => "true";

        "$(sara_data.slurm[backup_dir])/db.sql"
            rename => rotate("$(sara_data.slurm[backup_config][copies])");

        # Can be removed 2/sep/2020
        #"$(sara_data.slurm[backup_dir])/$(sara_data.slurm[ClusterName]).cfg"
            #rename => rotate("$(sara_data.slurm[AssocBackup][copies])");

    commands:
        "$(cmd)"
            comment => "Redirect must be in the args section",
            contain => in_shell,
            args => "> $(sara_data.slurm[backup_dir])/db.sql";

        # Can be removed 2/sep/2020
        #"$(sara_data.slurm[AssocBackup][cmd])"
        #    contain => in_shell_and_silent,
        #    args => "$(sara_data.slurm[ClusterName]) file=$(sara_data.slurm[backup_dir])/$(sara_data.slurm[ClusterName]).cfg";

    reports:
        any::
            "$(this.bundle): $(cmd) > $(sara_data.slurm[backup_dir])/db.sql"
                 if => "DEBUG|DEBUG_slurm|DEBUG_$(this.bundle)";

        # Can be removed 2/sep/2020
        #    "$(this.bundle): $(sara_data.slurm[AssocBackup][cmd])  $(sara_data.slurm[backup_dir])/$(sara_data.slurm[ClusterName]).cfg"
        #         if => "DEBUG|DEBUG_slurm|DEBUG_$(this.bundle)";
}

bundle agent slurm_logrotate()
{
    vars:
        any::
            "logrotate_files" slist => { "slurmd", "slurmdbd", "slurmctld" };

        SLURM_CLIENT::
            "log_files" slist => { "slurmd.log" };
        SLURM_SERVER::
            "log_files" slist => { "slurmdbd.log ", "slurmctld.log" };

    classes:
        "logrotate" or => {
            SLURM_CLIENT,
            SLURM_SERVER,
          };

    files:
        any::
            "/etc/logrotate.d/$(logrotate_files)"
                comment => "remove files causes unnecessary restart of daemon",
                delete => tidy;

        logrotate::
            "$(slurm.log_dir)/$(log_files)"
                rename  => rotate("7");

    reports:
        DEBUG_slurm|DEBUG::
            "$(this.bundle): has been called";
}

bundle agent slurm_spank_templates()
{
    vars:
        any::
            "files" slist => getindices("sara_data.slurm[spank_templates]");

    classes:
        any::
            "run_bundle_$(files)" expression => isvariable("sara_data.slurm[spank_templates][$(files)][run_bundle]"),
                comment => "Must we run a bundle when a file has been changed";

            "run_class_any_$(files)" expression => not(
                isvariable("sara_data.slurm[spank_templates][$(files)][run_class]")
                ),
                comment => "No run_class is set for file, so always run_bundle if defined";

            "run_class_expr_$(files)" expression => "$(sara_data.slurm[spank_templates][$(files)][run_class])",
                comment => "This class will be set if the expression(s) in the variable are true";

    methods:
        "" usebundle => sara_mustache_copy("slurm", "@(slurm_spank_templates.files)");

    files:
        "$(sara_data.slurm[spank_templates][$(files)][dest])"
            create => "true",
            edit_template => "$(def.node_template_dir)/slurm/$(files)",
            template_method => "mustache",
            template_data => "@(sara_data.slurm[spank_templates][$(files)][data])",
            classes => results("namespace", "SLURM_$(sara_data.slurm[spank_templates][$(files)][dest])");

        "$(sara_data.slurm[spank_templates][$(files)][dest])"
            perms => mog("$(slurm.mode)", "$(slurm.owner)", "$(slurm.group)");

    methods:
        "" usebundle => "$(sara_data.slurm[spank_templates][$(files)][run_bundle])",
            ifvarclass => and(
                canonify("SLURM_$(sara_data.slurm[spank_templates][$(files)][dest])_repaired"),
                canonify("run_bundle_$(files)"),
                or(
                    canonify("run_class_any_$(files)"),
                    canonify("run_class_expr_$(files)")
                  )
                );

    reports:
        "$(this.bundle) $(files) : $(sara_data.slurm[spank_templates][$(files)][dest])"
             if => "DEBUG|DEBUG_slurm|DEBUG_$(this.bundle)";
}

bundle agent slurm_generate_systemd_services()
{
    vars:
        any::
            "template_2_destination" data => parsejson('{
                    "slurmctld.service.mustache": "$(systemd.dir)/slurmctld.service",
                    "slurmd.service.mustache": "$(systemd.dir)/slurmd.service",
                    "slurmdbd.service.mustache": "$(systemd.dir)/slurmdbd.service",
            }');

            "template_files" slist => getindices("template_2_destination");

    methods:
        any::
            "" usebundle => sara_mustache_copy("slurm", "@(template_files)"),
                comment => "Copy the slurm systemd templates";

            "" usebundle => sara_mustache_cf_data_2_file("slurm",
                    "$(template_files)",
                    "$(template_2_destination[$(template_files)])",
                    ""),
                comment => "generate systemd config for slurmd";

    commands:
        any::
            "$(paths.path[systemctl]) daemon-reload"
                comment => "If service file has been changed reload systemd daemon",
                if => or(
                        canonify("sara$(template_2_destination[$(template_files)])")
                    );

            "$(slurm.restart_client)"
                comment => "If service file has been changed restart slurmd",
                if => or(
                        canonify("sara$(template_2_destination[$(template_files)])")
                    );

    reports:
        any::
            "$(this.bundle): $(systemd.dir)/$(template_2_destination[$(template_files)]) this has been changed"
                if => or(
                        canonify("sara$(template_2_destination[$(template_files)])")
                    );
}
@if minimum_version(99.9)

# slurm

Source: [slurm.cf](/services/slurm.cf)

This bundle will generate these/this file(s) from mustache templates:
 * debian in `/etc/slurm-llnl`
 * centos in `/etc/slurm`
    * cgroup.conf
    * cgroup_allowed_devices_file.conf
    * gres.conf
    * nodes.conf
    * plugstack.conf
    * slurm.conf
    * slurmdb.conf
    * topology.conf

If one of the files is changed then the following ""class"" will be set:
 * sara_etc_slurm_llnl_slurm_conf (debian)
 * sara_etc_slurm_slurm_conf (centos)

The class name for the other files has the same format.

These templates are located in:
 * templates/slurm
 * templates/slurm/json

The following `classes` can be set via def.cf/json:
 * BACKUP: When set make a backup of the SQL sever
 * CLIENT: Configure the machine as client
 * LOGROTATE: When set disable logrotate configs and use cfengne logrotate (no daemons restart)
 * SLURMD_DISABLE: Disable the slurmd systemd service
 * SERVER: Configure the machine as server
 * SUBMIT: Configure the machine as submit host
 * SYSTEMD_SERVICES: generate the slurm systemd unit files.
 * TARBALL: Slurm software will be installed as tarball instead of system packages + generate slurm systemd unit files

The following json variables can be set in def.cf/json to  invoke files bundles:
 * copy_files: See [files.cf](/masterfiles/lib/surfsara/files.cf)
 * copy_dirs: See [files.cf](/masterfiles/lib/surfsara/files.cf)
 * install_tarballs: See [files.cf](/masterfiles/lib/surfsara/files.cf)

## Usage

The bundle can be run via:
 * `def.sara_services_enabled`
```json
"vars": {
    "sara_services_enabled": [
            "...",
            "slurm",
            "..."
    ]
}
```

The bundle will always read the [default.json](/templates/slurm/json/default.json) file
and extra json file(s) can be specified via:
 * def.cf
```
vars:
    any::
        "slurm_json_files" slist => { "cluster.json" };
```

The variable must be ""slurm_json_files"" and with this setup 1 extra json file will be merged.

### DEBUG

if you want to debug this bundle set the `DEBUG_slurm` class, eg:
 * `-DDEBUG_slurm`

## def.cf/json

See [default.json](/templates/slurm/json/default.json) what the default values are and
which variables can be overriden.

Here are some examples how to use it:
 * specify resolv configuration in def.cf:
```
vars:
    "slurm_json_files" slist => { "cluster.json" };
```

 * override config settings in def.cf
```
vars:
    "slurm" data => parsejson( '{ "PrologFlags": "contains" }' );
```

 * override config settings in ''def.json'':
```json
"vars": {
    "slurm": {
            "json_files": [ "mona.json" ]
        },
}
```

 * override variable config setting in ''def.json''
```json
"slurm": {
    "PrologFlags": "contains"
}
```

### spank plugins

Slurm Plug-in architecture for Node and job Kontrol (SPANK)
plugins are configured via the `plugstack.conf` file.

This generations of the spank plugin configuration file(s) can be
done dynamically,eg:
```#json
 "spank_templates": {
        "private_tmpdir.mustache" : {
            "dest": "$(slurm.plugstack_dir)/private-tmpdir.conf",
            "data": {
                "config": [
                    "required /opt/slurm/lib/private-tmpdir.so base=/scratch/slurm  mount=/var/tmp mount=/tmp mount=/scratch"
                ]
            }
            "run_bundle': <not_required> when set run the specified bundle"
            "run_class': <not_required> when set run the specified bundle only for the specified classses (string or list)"
        }
    },
```

This will generate the `private-tmpdir.conf` file with the aid of the specified mustache file and
json data.
@endif
