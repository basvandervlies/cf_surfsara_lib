#
## jupyterhub.cf @SURF
#
bundle common jupyterhub
{
    vars:
        any::
            "template_2_destination" data => parsejson('
                    {
                        "apache.mustache"  : "no_destination_defined_is_generated",
                        "script.mustache"  : "no_destination_defined_is_generated",
                        "systemd.mustache"  : "no_destination_defined_is_generated"
                    }
            ');
}

bundle agent jupyterhub_autorun()
{
    meta:
        "tags" slist => { "service_jupyterhub", "template_jupyterhub" };

    methods:
        any::
            "" usebundle => jupyterhub_install();

        JUPYTERHUB_SERVER::
            "" usebundle => jupyterhub_config();
            "" usebundle => jupyterhub_daemons_check();
            "" usebundle => jupyterhub_remove_inactive_hubs();
}

bundle agent jupyterhub_install()
{
    classes:
        any::
            "tarball_set" expression => isvariable("scl.jupyterhub[install_tarballs]");

    files:
        "$(scl.jupyterhub[dir])/."
            perms => mog("0755", "root", "root"),
            create=> "true";

    methods:
        tarball_set::
            "" usebundle => scl_service_install_tarballs("jupyterhub");

}

bundle agent jupyterhub_daemons_check()
{
    vars:
            "names" slist => getindices("scl.jupyterhub[hubs]");
            "daemon_$(names)" string => "^$(scl.jupyterhub[dir]).*$(names).py";

    processes:
        any::
            "$(daemon_$(names))"
                comment => "Check if the jupyterhub config is runnig",
                process_count => check_range("$(names)", "1", "1"),
                process_select => scl_select_parent_process("1");

    commands:
        any::
            "$(paths.path[systemctl])  restart jupyterhub-$(names)"
                if => or(
                    canonify("$(names)_out_of_range"),
                    JUPYTERHUB_RESTART_SCHEDULE
                    );

    reports:
        any::
            "$(this.bundle): $(daemon_$(names))"
                if => "DEBUG|DEBUG_jupyterhub|DEBUG_$(this.bundle)";
}

bundle agent jupyterhub_config()
{
    vars:
        any::
            "current" string => "$(scl.jupyterhub[current])";
            "template_files" slist => getindices("jupyterhub.template_2_destination");

            "names" slist => getindices("scl.jupyterhub[hubs]");

            "data_$(names)" data => mergedata(
                "scl.jupyterhub[config_default]", "scl.jupyterhub[hubs][$(names)]" ),
                comment => "Merge the default hub settings with the one defined in the config";

            "data_$(names)" data => mergedata("data_$(names)", '{ "name": "${names}" }'),
                comment => "We need the hub name in json data";

    classes:
        any::
            "copy_dirs_set" expression => isvariable("scl.jupyterhub[copy_dirs]");

    files:
        any::
            "$(scl.jupyterhub[bin_dir])/."
                create => "true",
                perms => mog("0700", "root", "root"),
                move_obstructions => "true";

            "$(scl.jupyterhub[dir])/current"
                link_from => ln_s("$(scl.jupyterhub[dir])/$(scl.jupyterhub[current])"),
                move_obstructions => "true";

            "$(scl.jupyterhub[etc_dir])/."
                create => "true",
                perms => mog(
                    "$(scl.jupyterhub[mog_dirs][0])",
                    "$(scl.jupyterhub[mog_dirs][1])",
                    "$(scl.jupyterhub[mog_dirs][2])"
                ),
                move_obstructions => "true";

            "$(scl.jupyterhub[state_dir])/$(names)/."
                comment => "Create jupyterhub state dir for this config",
                create => "true",
                perms => mog("0700", "root", "root"),
                move_obstructions => "true";

            "$(scl.jupyterhub[bin_dir])/$(names).sh"
                perms => mog("0700", "root", "root");

            "$(scl.jupyterhub[etc_dir])/$(names).py"
                perms => mog(
                    "$(scl.jupyterhub[mog_config_files][0])",
                    "$(scl.jupyterhub[mog_config_files][1])",
                    "$(scl.jupyterhub[mog_config_files][2])"
                );

    methods:
        any::
            "" usebundle => scl_mustache_copy("jupyterhub", "@(template_files)"),
                comment => "Copy the jupyterhub static templates";


            "" usebundle => scl_mustache_copy("jupyterhub", "$(scl.jupyterhub[hubs][$(names)][template_file])" ),
               comment => "Each hub define its own template file";


            "" usebundle => scl_mustache_cf_data_2_file("jupyterhub",
                    "systemd.mustache",
                    "$(systemd.dir)/jupyterhub-$(names).service",
                    "jupyterhub_config.data_$(names)"
                    ),
                comment => "generate per config a systemd unit file loop over $(names)";

            "" usebundle => scl_mustache_cf_data_2_file("jupyterhub",
                    "apache.mustache",
                    "$(scl.apache[configdir])/local.d/jupyterhub_generated.conf",
                    "scl.jupyterhub"
                    ),
                comment => "generate jupyterhub apache config file";

            "" usebundle => scl_mustache_cf_data_2_file("jupyterhub",
                    "script.mustache",
                    "$(scl.jupyterhub[bin_dir])/$(names).sh",
                    "jupyterhub_config.data_$(names)"
                    ),
                comment => "generate start script for hubm used by systemd unit file";

            "" usebundle => scl_mustache_cf_data_2_file("jupyterhub",
                    "$(scl.jupyterhub[hubs][$(names)][template_file])",
                    "$(scl.jupyterhub[etc_dir])/$(names).py",
                    "jupyterhub_config.data_$(names)"
                    ),
                comment => "generate start script for hub used by systemd unit file";

            "" usebundle => jupyterhub_restart("$(names)"),
                comment => "restart jupyterhub service if config file has been changed",
                if => or(
                    canonify("scl$(scl.jupyterhub[etc_dir])/$(names).py")
                    );

        copy_dirs_set::
            "" usebundle => scl_service_copy_dirs("jupyterhub");

    services:
        any::
            "jupyterhub-$(names)"
                comment => "enable and start the jupyterhub service if systemd unit has been changed",
                service_policy => "start",
                if => canonify("scl$(systemd.dir)/jupyterhub-$(names).service");

    commands:
        any::
            "$(apache.restart)"
                comment => "Restart apache if we generate a new jupyterhub apache file",
                if => canonify("scl$(scl.apache[configdir])/local.d/jupyterhub_generated.conf");

            "$(paths.path[systemctl]) daemon-reload"
                comment => "If service file has been changed reload systemd daemon",
                if => canonify("scl$(systemd.dir)/jupyterhub-$(names).service");
}


bundle agent jupyterhub_restart(name)
{
    commands:
        any::
            "$(paths.path[systemctl]) restart jupyterhub-$(name)";
}

bundle agent jupyterhub_remove_inactive_hubs
{
    vars:
        "current" slist => sort(getindices("scl.jupyterhub[hubs]"), "lex");
        "generated" slist => sort(lsdir("$(scl.jupyterhub[state_dir])","[a-zA-Z]+.*", "false"), "lex");
        "inactive" slist => difference(generated, current);

    services:
        any::
            "jupyterhub-$(inactive)"
                comment => "stop the HUB server",
                service_policy => "stop",
                classes => results("bundle", "hub_$(inactive)");

    methods:
        any::
            "" usebundle => jupyterhub_rm("$(inactive)"),
                if => or(
                            canonify("hub_$(inactive)_kept"),
                            canonify("hub_$(inactive)_failed")
                        );

    reports:
        any::
            "$(this.bundle):$(inactive)"
                if => or(
                            canonify("hub_$(inactive)_kept"),
                            canonify("hub_$(inactive)_failed")
                        );
}

bundle agent jupyterhub_rm(name)
{
   files:
        any::
            "$(systemd.dir)/jupyterhub-$(name).service"
                delete => tidy;

            "$(scl.jupyterhub[bin_dir])/$(name).sh"
                delete => tidy;

            "$(scl.jupyterhub[etc_dir])/$(name).py"
                delete => tidy;

    methods:
        any::
            "" usebundle => rm_rf("$(scl.jupyterhub[state_dir])/$(name)");

    reports:
        "$(this.bundle): remove hub setup $(name)
rm $(systemd.dir)/jupyterhub-$(name).service
rm $(scl.jupyterhub[bin_dir])/$(name).sh
rm $(scl.jupyterhub[etc_dir])/$(name).piiiiy
rm -rf $(scl.jupyterhub[state_dir])/$(name)"
                if => "DEBUG|DEBUG_jupyterhub|DEBUG_$(this.bundle)";
}

@if minimum_version(99.9)
# JUPYTERHUB

Source: [jupyterhub.cf](/services/jupyterhub.cf)


This will install the jupyterhub software on a node and we can generate multiple jupyterhub setups on
one node. We mostly use jupyterhub to submit jobs to the batch system and start the notebook on a compute
node. Each jupyterhub has its own parameters for submitting jobs. Apache is used as frontend web server,
see [apache.cf](/services/apache.cf). This service bundle will also generate the apache configuration
file for you.

The following json variables can be set in def.cf/json to invoke files bundles:
 * copy_dirs: See [files.cf](/masterfiles/lib/scl/files.cf)
 * install_tarballs: See [files.cf](/masterfiles/lib/scl/files.cf)

The following clases can be set via def.cf/json:
 *  `RESTART_SCHEDULE`:  Restart the jupyterhub services on specified time(s), eg:
```
"RESTART_SCHEDULE": [
    "Hr00.Min00_05"
]
```

The definition of jupyterhub configuration is illustrated via an example:
```
    "config_default": {
        "admin_groups": [
            "'lisa_surfsara'"
        ],
        "dir": "$(scl.jupyterhub[dir])",
        "pam_service_file": "jupyterhub",
        "start_timeout": "3600",
        "url_api": "http://127.0.0.1:81",
        "url_bind": "http://127.0.0.1:80",
        "url_hub_bind": "http://:82",
        "url_ws": "ws://127.0.0.1:80"
    },
    "hubs": [
        "course": {
            "number": "01",
            "template_file": "jupyterhub_course.mustache"
        },
        "prod": {
            "number": "02",
            "pam_service_file": "jupyterhub-prod",
            "template_file": "jupyterhub_prod.mustache"
        }
    ],
```

The `config_default` section are variables used in all jupyterhub configuration files but you can
override them in the `hubs` section. In the `hubs` section there are 2 jupyterhubs configurations
defined:
 * course
 * prod

Each configuration must use a unique port number (the number is appended to the url, eg 8101).
For the jupyterhub `course` it will generate:
 * /etc/systemd/system/jupyterhub-course unit file
 * /opt/jupyterhub/bin/course.sh (used in the systemd unit file)
 * /opt/jupyterhub/etc/course.py (with the aid of `jupyterhub-course.mustache`)
 * start the jupyterhub server
 * This is also done for the other configurarions.

The apache configurarion is also generated with the right port numbers and apache will be restarted. Now
the jupyterhubs are accessible with:
 * https://<name>/course
 * https://<name>/prod

## Usage

The bundle can be run via:
 * `def.scl_services_enabled`
```json
"vars": {
    "scl_services_enabled": [
            "...",
            "jupyterhub",
            "..."
    ]
}
```

The bundle will always read the [default.json](/templates/jupyterhub/json/default.json) file
and extra json file(s) can be specified via:
 * def.cf
```
vars:
    any::
        "jupyterhub_json_files" slist => { "lisa.json" };
```

The variable must be ''jupyterhub_json_files'' and with this setup 1 extra json file will be merged.

### DEBUG

If you want to debug this bundle set the `DEBUG_jupyterhub` class, eg:
 * `-DDEBUG_jupyterhub`

## def.cf/json

See [default.json](/templates/jupyterhub/json/default.json) what the default values are and
which variables can be overriden.

Here is an example to check also the apache binary (def.cf):
```
vars:
    "jupyterhub_json_files" slist => { "lisa.json" ];
```

Same can also be set in `def.json`
```json
"vars": {
    "jupyterhub": {
        "dir": "/sw/jupyterhub"
    }
}
@endif
