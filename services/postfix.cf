#
## postfix.cf @SURF
#
bundle common postfix() {
    vars:
        debian::
            "packages"  data  => parsejson('
                    {
                        "install": {
                            "bsd-mailx" : "",
                            "postfix" : "",
                            "postfix-ldap" : ""
                        },
                        "purge": {
                            "heirloom-mailx": ""
                        }
                    }
                ');
            "service_name"      string => "postfix";
            "daemon_name"       string => "/usr/lib/postfix/sbin/master";

        redhat::
            "packages"  data  => parsejson('
                    {
                        "install": {
                            "mailx" : "",
                            "postfix" : ""
                        },
                        "remove": {
                            "heirloom-mailx": ""
                        }
                    }
                ');
            "service_name"      string => "postfix";
            "daemon_name"       string => "/usr/libexec/postfix/master";

        sles::
            "packages"  data  => parsejson('
                    {
                        "install": {
                            "mailx" : "",
                            "postfix" : ""
                        }
                    }
                ');
            "service_name"      string => "postfix";
            "daemon_name"       string => "/usr/lib/postfix/bin/master";

        any::
            "pflogsumm_dir"     string => "/opt/pflogsumm";
            "newaliases"        string => "/usr/bin/newaliases";
            "postmap_cmd"       string => "/usr/sbin/postmap";
            "config_dir"        string => "/etc/postfix/";
            "setpermissions"    string => "/usr/sbin/postfix set-permissions";

            ## Mustache templates
            "template_2_destination" data => parsejson('
                {
                    "aliases.mustache" : "/etc/aliases",
                    "main.cf.mustache" : "/etc/postfix/main.cf",
                    "master.cf.mustache" : "/etc/postfix/master.cf",
                    "dynamicmaps.cf.mustache" : "/etc/postfix/dynamicmaps.cf",
                    "canonical_map.mustache" : "/etc/postfix/canonical_map",
                    "header_checks.mustache": "/etc/postfix/header_checks",
                    "transport.mustache": "/etc/postfix/transport"
                }
            ');

        systemd::
            "postfix_restart" string => "$(paths.path[systemctl]) restart $(service_name)";
            "postfix_reload"  string => "$(paths.path[systemctl]) reload $(service_name)";

        !systemd::
            "postfix_restart" string => "$(paths.path[service]) $(service_name) restart";
            "postfix_reload" string => "$(paths.path[service]) $(service_name) reload";
}

bundle agent postfix_pflogsumm()
{
    commands:
        "/opt/pflogsumm/sara_pflogsumm";
}

bundle agent postfix_autorun()
{
    meta:
        "tags"  slist => { "service_postfix", "template_postfix" };

    classes:
        "postfix_copy_dirs_set" expression => isvariable("scl.postfix[copy_dirs]");

    methods:
        any::
            ""  usebundle => postfix_install();
            ""  usebundle => scl_service_copy_dirs("postfix"),
                    if => "postfix_copy_dirs_set";
            ""  usebundle => postfix_config();
            ""  usebundle => postfix_daemons_check();

        POSTFIX_VIRTUAL_MAPS::
            "" usebundle => postfix_virtual_alias_maps();

        POSTFIX_PFLOGSUMM.(Hr07.Min00_05)::
            "" usebundle => postfix_pflogsumm();
}

bundle agent postfix_install()
{
    methods:
        "" usebundle => scl_service_packages("postfix", "@(postfix.packages)");
}

bundle agent postfix_daemons_check
{
    processes:
        linux::
            "$(postfix.daemon_name)"
                comment         => "Check if the required daemon is running",
                process_count   => check_range("$(postfix.daemon_name)", "1", "1"),
                process_select  => scl_select_parent_process("1");

    commands:
        linux::
            "$(postfix.setpermissions)"
                if => canonify("$(postfix.daemon_name)_out_of_range");

            "$(postfix.postfix_restart)"
                if => canonify("$(postfix.daemon_name)_out_of_range");

    reports:
        linux::
            "$(this.bundle): $(postfix.daemon_name) with PPID:1 not running"
                if => canonify("$(postfix.daemon_name)_out_of_range");
}

bundle agent postfix_config
{
    classes:
        "POSTFIX_NEWALIAS"
            if => or(
                canonify("scl/etc/aliases")
            );

        "POSTFIX_RELOAD"
            if => or(
                canonify("scl/etc/postfix/main.cf"),
                canonify("scl/etc/postfix/master.cf"),
                canonify("scl/etc/postfix/dynamicmaps.cf")
        );

    files:
        "/etc/postfix/main.cf"
            perms => mog("0644", "root", "root"),
                comment => "Must be readable for all users";

    methods:
        "" usebundle => scl_mustache_autorun("postfix");

    commands:
        POSTFIX_NEWALIAS::
            "$(postfix.newaliases)";

        POSTFIX_RELOAD::
            "$(postfix.postfix_reload)";

        scl_etc_postfix_transport::
            "$(postfix.postmap_cmd) $(postfix.config_dir)/transport"
                comment => "This is a hashed file, so we need to use postmap after a change";

        scl_etc_postfix_header_checks::
            "$(postfix.postmap_cmd) $(postfix.config_dir)/header_checks"
                comment => "This is a hashed file, so we need to use postmap after a change";

        scl_etc_postfix_canonical_map::
            "$(postfix.postmap_cmd) $(postfix.config_dir)/canonical_map"
                comment => "This is a hashed file, so we need to use postmap after a change";
}

bundle agent postfix_virtual_alias_maps_expand(virtual_map_id)
{
    classes:
        "needs_postmap_hash" expression => strcmp("$(scl.postfix[virtual_alias_maps][$(virtual_map_id)][protocol])", "hash");
        "needs_postmap_lmdb" expression => strcmp("$(scl.postfix[virtual_alias_maps][$(virtual_map_id)][protocol])", "lmdb");
        "needs_postmap"      if => or(
            "needs_postmap_hash",
            "needs_postmap_lmdb"
        );

    files:
        "$(scl.postfix[virtual_alias_maps][$(virtual_map_id)][dest])"
            create => "true",
            edit_template => "$(def.node_template_dir)/postfix/$(scl.postfix[virtual_alias_maps][$(virtual_map_id)][template])",
            template_method => "mustache",
            template_data => "@(scl.postfix[virtual_alias_maps][$(virtual_map_id)][data])",
            classes => results("namespace", "postfix_virtual_alias_maps_expand_$(virtual_map_id)");

    commands:
        "$(postfix.postmap_cmd) $(scl.postfix[virtual_alias_maps][$(virtual_map_id)][dest])"
            if => and(
                "needs_postmap",
                "postfix_virtual_alias_maps_expand_$(virtual_map_id)_repaired"
            );
}

bundle agent postfix_virtual_alias_maps()
{
    vars:
        "template_maps" slist => getindices("scl.postfix[virtual_alias_maps]");
        "files" slist => { };
        "files" data => mergedata(files, parsejson('[ "$(scl.postfix[virtual_alias_maps][$(template_maps)][template])" ]') ),
            comment => "scl_mustache_copy expects a slist";

    methods:
        "" usebundle => scl_mustache_copy("postfix", "@(postfix_virtual_alias_maps.files)");
        "" usebundle => postfix_virtual_alias_maps_expand("$(template_maps)");

    reports:
        "$(this.bundle) Template : $(files)"
             if => "DEBUG|DEBUG_postfix|DEBUG_$(this.bundle)";
}

@if minimum_version(99.9)
# POSTFIX

Source: [postfix.cf](/services/postfix.cf)

This bundle will check and configure postfix for Debian and CentOS. The following
files will be generated from mustache templates:
 * /etc/aliases
 * /etc/postfix/main.cf
 * /etc/postfix/master.cf
 * /etc/postfix/dynamicmaps.cf
 * /etc/postfix/recipient_canonical
 * /etc/postfix/header_checks
 * /etc/postfix/transport

Created by the bundle postfix_virtual_alias_map()
 * /etc/postfix/virtual_alias_maps.cf

If one of the files is changed then the following ''class'' will be set:
 * scl_etc_aliases
 * scl_etc_postfix_main_cf
 * scl_etc_postfix_master_cf
 * scl_etc_postfix_dynamicmaps_cf
 * scl_etc_postfix_recipient_canonical
 * scl_etc_postfix_header_checks
 * scl_etc_postfix_transport

These files will be generated with the aid of mustache templates with json data.
the templates are located in:
 * templates/postfix/
 * templates/postfix/json


## USAGE

The bundle can be run via:
 * `def.scl_services_enabled`
```
"vars": {
    "scl_services_enabled": [
            "...",
            "postfix",
            "..."
    ]
}
```

The bundle will aways read the [default.json](/templates/postfix/json/default.json) file
and extra json file(s) can be specified via:
 * def.cf
```
vars:
    any::
        "postfix_json_files" slist => { "zimbra.json" };
```

The variable must be ''postfix_json_files'' and with this setup 1 extra json file will be  merged.

### Classes

For more complex Postfix servers (for example a mailhandler) some additonal classes have been added
to the mustache template for more strict handling of mail and enabling TLS for receiving emails.

Also some basic support for DOVECOT can be enabled. These classes are:
 * POSTFIX_STRICT_HANDLING : Limits the amount of mail per second, and adds more restrictions for accepting mail from other hosts
 * POSTFIX_REJECT_LOCAL : Reject all mail with the destination localhost
 * POSTFIX_RECEIVE_TLS : Enable more TLS options to add TLS support on the smtp port for receiving mail
 * POSTFIX_LOWER_34 : When you are using a Postfix version of 3.4 or lower
 * POSTFIX_DOVECOT : Enable basic support for authentication via Dovecot
 * POSTFIX_PFLOGSUMM : Run the pflogsumm command

All of the above classes can be defined in the def.cf/json files.

### DEBUG

if you want to debug this bundle set the `DEBUG_postfix` class, eg:
 * `-DDEBUG_postfix`

## def.cf/json

See [default.json](/templates/postfix/json/default.json) what the default values are and
which variables can be overriden

Here are some examples how to use it:
 * specify postfix configuration in def.cf:
```
vars:
    "postfix_json_files" slist => { "zimbra.json" };
```
 * Set/Override options variable in a *def.json* file:
```json
"vars": {
    "postfix" : {
        "alias_root_email_address": "noreply@surfsara.nl",
        "json_files": [ "zimbra.json" ],
        "file_aliases_lines": [ {"name": "beowulf", "value": "|\"/usr/bin/run_email2trac --project beowulf\""} ]
    }
}
```

 * override server setting in def.cf
```
vars:
    "postfix" data => parsejson( '{ "alias_root_email_address":  "file_aliases_lines"  }' );
```


### VIRTUAL_ALIAS_MAPS

with this bundle you can define youw own mustache template with json data for creating
the virtual alias map file(s). The format of the `virtual_alias_maps` is:
```
"virtual_alias_maps": {
    "mustache_file": {
        "data": {},
        "delimiter": "",
        "dest": "",
        "protocol": ""
    }
}
```
An example for defining an LDAP alias map for mustache (.forward is fetch from LDAP). This will copy the template
`ldap_aliases_map.mustache` and expand it with the give json data.
```
"postfix": {
    "classes" : {
        "VIRTUAL_MAPS": [ "mta.example.com" ],:
    },
    "virtual_alias_maps": {
        "ldap_aliases_map.mustache": {
            "delimiter": ":",
            "dest": "/etc/postfix/virtual_alias_maps.cf",
            "protocol": "ldap",
            "data": {
                "bind": true,
                "bind_options": {
                    "dn": "<your_dn>",
                    "pw": "<your_bind_password>"
                },
                "port": "636",
                "query_filter": "(uid=%s)",
                "result_attribute": "mail",
                "search_base": "ou=Users,dc=example,dc=com",
                "server": "ldaps://ldap.example.com"
            }
        }
    }
}
```
@endif
