#
#  SURFsara packages.cf
#
@if minimum_version(99.9)
# packages.cf

Source: [packages.cf](/masterfiles/lib/scl/packages.cf)

## scl_service_packages
This bundle handles  the service packages. The service bundle defines the default
package(s) to install/remove and/or purge. It can be overriden with json data, eg:
```
"vars": {
    "ssh": {
        "packages": {
            "install": {
                "openssh-server": "[version]"
            }
        }
    }
}
```

This will install the 'openssh-server' package and override the package definitions in the service bundle ssh.
The `[version]` can be empty `""`. It will install any version of the package.

Another example is to install and remove ssh packages in 'def.json':
```
"vars": {
    "ssh": {
        "packages": {
            "install": {
                "openssh-server": "[version]"
            },
            "remove": {
                "openssh-blacklist": "[version]"
            }
        }
    }
}
```

The following package actions are implemented:
 * install: install the package
 * install_backports: install the package from backports (Debian)
 * purge: delete the package and all it configuration files
 * remove: delete the package and preserve the configuration files
@endif
bundle agent scl_service_packages(service_name, data)
{
    vars:
       json_packages_specified::
            "pkg_data" data => mergedata("{}", "scl.$(service_name)[packages]");
            "debug_str" string => "Use 'def.json' service package definition";

       !json_packages_specified::
            "pkg_data" data => mergedata("{}", "data");
            "debug_str" string => "Use bundle service packages definition";


        any::
            "actions" slist => getindices("pkg_data");
            "pkg_bundles" slist => maplist("scl_service_packages_$(this)", @(actions));

    classes:
        "json_packages_specified" expression => isvariable("scl.$(service_name)[packages]"),
            comment => "Do we override the bundle services package with def.json settings";

    methods:
        "" usebundle => $(pkg_bundles)("$(service_name)", "@(scl_service_packages.pkg_data)"),
            comment => "package bundle implemented install/remove/purge";

    reports:
        "$(this.bundle)($(service_name)):$(actions):$(debug_str)"
            if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
}


bundle agent scl_service_package(service_name, package, version, action, options)
{
    classes:
        "version_specified" expression => not(strcmp("", "$(version)"));
        "options_specified" expression => not(strcmp("", "$(options)"));

    vars:
        "debug_str" string => ifelse(
            "version_specified.options_specified", "version: '$(version)', package_options: '$(options)'",
            "version_specified.!options_specified", "version: '$(version)', package_options: 'NA'",
            "!version_specified.options_specified", "version: 'NA', package_options: '$(options)'",
            "version: 'NA', package_options: 'NA'");

    packages:
        !version_specified::
            "$(package)"
                policy => "$(action)",
                package_method => generic,
                options => { "$(options)" };

        version_specified::
            "$(package)"
                policy => "$(action)",
                package_method => generic,
                version => "$(version)",
                options => { "$(options)" };

    reports:
        any::
            "$(this.bundle)($(service_name)):$(package):$(action)  with $(debug_str)"
                if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
}

bundle agent scl_service_packages_install(service_name, pkg_data)
{
    vars:
        any::
            "packages" slist => getindices("pkg_data[install]");

    methods:
        any::
            "" usebundle => scl_service_package("$(service_name)",
                "$(packages)", "$(pkg_data[install][$(packages)])", "present", "");
}

bundle agent scl_service_packages_install_backports(service_name, pkg_data)
{
    vars:
        any::
            "packages" slist => getindices("pkg_data[install_backports]");

@if minimum_version(3.12)
        debian::
            "release" string => regex_replace("$(sys.os_release[VERSION])", ".*\((\w+).*", "\1", "");
            "pkg_options" string => "--target-release=$(release)-backports";
@endif

        (!debian|cfengine_3_7|cfengine_3_10)::
            "pkg_options" string => "";

    methods:
        any::
            "" usebundle => scl_service_package("$(service_name)",
                "$(packages)", "$(pkg_data[install_backports][$(packages)])", "present", "$(pkg_options)");

    reports:
        !debian::
            "$(this.bundle) is only supported on Debian, falling back to normal install"
                if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
}

bundle agent scl_service_packages_remove(service_name, pkg_data)
{
    vars:
        any::
            "packages" slist => getindices("pkg_data[remove]");

    methods:
        any::
            "" usebundle => scl_service_package("$(service_name)",
                "$(packages)", "$(pkg_data[remove][$(packages)])", "absent", "");
}

bundle agent scl_service_packages_purge(service_name, pkg_data)
{
    vars:
        any::
            "packages" slist => getindices("pkg_data[purge]");

        debian::
            "pkg_options" string => "--purge";
        !debian::
            "pkg_options" string => "";

    methods:
        any::
            "" usebundle => scl_service_package("$(service_name)",
                "$(packages)", "$(pkg_data[purge][$(packages)])", "absent", "$(pkg_options)");
}

###### Obsolete will be deleted
##
bundle agent scl_install_service_packages(service_name, default_packages)
{
    vars:
        "package_data" data => mergedata("{}", "scl.$(service_name)[packages]"),
            if => and(
                "scl_$(service_name)_packages_specified"
                );

        "package_data" data => mergedata("{}", "default_packages"),
            if => and(
                "!scl_$(service_name)_packages_specified"
                );

        "packages_data_str" string => format("%S", "package_data"),
            if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";

        "packages" slist => getindices("package_data");


    classes:
        "scl_$(service_name)_packages_specified" expression => reglist("@(scl.$(service_name)[packages])", ".*");
        #"scl_$(service_name)_packages_specified" expression => regarray("@(scl.$(service_name)[packages])", ".*");


    methods:
        any::
            "" usebundle => scl_install_service_package("$(service_name)", "$(packages)", "$(package_data[$(packages)])"),
                comment => "Install the package with version";

    reports:
        "$(this.bundle)($(service_name)):$(packages_data_str)"
            if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
}

bundle agent scl_install_service_package(service_name, package, version)
{
    classes:
        "version_specified" expression => not(strcmp("", "$(version)"));

    packages:
        !version_specified::
            "$(package)"
                policy => "present";

        version_specified::
            "$(package)"
                policy => "present",
                version => "$(version)";

    reports:
        !version_specified::
            "$(this.bundle)($(service_name)): Install package: $(package) with version: 'not specified'"
                if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
        version_specified::
            "$(this.bundle)($(service_name)): Install package: '$(package)' with version: '$(version)'"
                if => "DEBUG_$(service_name)|DEBUG|DEBUG_MUSTACHE_JSON|DEBUG_MUSTACHE";
}
