#
## sudo.cf @SURF
#
bundle common sudo
{
    vars:
        any::
            "template_2_destination" data => parsejson('
                {
                    "sudo.mustache" : "$(surfsara_file)",
                }
            ');

        debian|centos::
            "file"              string => "/etc/sudoers";
            "dir"               string => "/etc/sudoers.d";
            "file_name"         string => "surfsara";
            "surfsara_file"     string => "$(dir)/$(file_name)";
            "owner"             string => "root";
            "group"             string => "root";
            "mode"              string => "0440";
}

bundle agent sudo_autorun()
{
    meta:
        "tags" slist => { "service_sudo", "template_sudo" };

    methods:
        any::
            "" usebundle => sudo_install();
            "" usebundle => sudo_config();

    reports:
        DEBUG_sudo::
            "$(this.bundle): surf sudo template run";
}

bundle agent sudo_install()
{
    vars:

        debian|centos::
            "packages" data => parsejson('
                {
                    "install": {
                        "sudo": "",
                    }
                }
            ');

    methods:
        any::
            "" usebundle => scl_service_packages("sudo", "@(sudo_install.packages)");
}

bundle agent sudo_config()
{
    vars:
        any::
            "sudo_config_file_class" string => canonify("scl$(sudo.surfsara_file)");
            "exclude_list"           slist  => { "$(sudo.file_name)" };

    methods:
        any::
            "" usebundle => scl_mustache_autorun("sudo");

    files:
        SUDO_ONLY_ONE_CONFIG_FILE::
            "$(sudo.dir)/.*"
                    comment         => "Remove all files in this directory, only one $(sudo.file_name) allowed",
                    delete          => tidy,
                    file_select     => exclude(@(exclude_list));

    reports:
        any::
            "$(this.bundle): $(sudo.file) file has changed"
               if => "$(sudo_default_file_class)";

        DEBUG_sudo.SUDO_ONLY_ONE_CONFIG_FILE::
            "$(this.bundle): Only 'surf' config allowed in '$(sudo.dir)'";

}
@if minimum_version(99.9)
# sudo

Source: [sudo.cf](/services/sudo.cf)

This bundle will generate this file from mustache template:
 * /etc/sudoers.d/surfsara

f one of the files is changed then the followong *class* will be set:
 * scl_etc_sudoers_d_surfsara

These files will be generated with the aid of mustache templates with json data.
the templates are located in:
 * templates/sudo/
 * templates/sudo/json

The default is to only allow one config file in `/etc/sudoers.d`:
 * surfsara

You can override it by disabling class `ONLY_ONE_CONFIG_FILE` in the
sudo json data secton, eg:
 * def.json
```json
"sudo": {
    "classes": {
        "ONLY_ONE_CONFIG_FILE": [ "unset" ]
    }
}
```
## Usage

The bundle can be run via:
 * `def.scl_services_enabled`
```json
"vars": {
    "scl_services_enabled": [
            "...",
            "sudo",
            "..."
    ]
}
```

The bundle will always read the [default.json](/templates/sudo/json/default.json) file
and extra json file(s) can be specified via:
 * def.cf
```
vars:
    any::
        "sudo_json_files" slist => { "lisa.json" };
```

The variable must be ''sudo_json_files'' and with this setup 1 extra json file will be  merged.

### DEBUG

If you want to debug these bundle set the `DEBUG_sudo` class, eg:
 * `-DDEBUG_sudo`

##  def.cf/json

See [default.json](/templates/sudo/json/default.json) what the default values are and
which variables can be overriden.

Here are some examples how to use it:
 * specify sudo configuration in def.cf:
```
vars:
    "sudo_json_files" slist => { "lisa.json" };
```

 * Set/Override variables in *def.json*:
```json
"sudo": {
    "USERHOST" : "r25n3, r25n4, batch1, login*, software*",
    "SOFTWARE" : "software*",
    "FILESERVER" : "fs*",
    "json_files": [ "lisa.json" ]
}
```

 * override variable in *def.cf*:
```
vars:
    "sudo" data => parsejson( '{ "USERHOST":  "r25n3, r25n4, batch1, login*, software*"  }' );

```
@endif
