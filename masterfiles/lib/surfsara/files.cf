#
## files.cf @SURFsara
#
## https://tracker.mender.io/browse/CFE-2434 bug in passing data containers with vars
#
@if minimum_version(99.9)
# files.cf

Source: [files.cf](/masterfiles/lib/surfsara/files.cf)

For copying files/dirs in the service bundle from the policy server we have defined 2 bundles:
 1. sara_service_copy_dirs
 1. sara_service_copy_files

## sara_service_copy_dirs(bundle_name)

This bundle will read the json attribute `copy_dirs` from the specified `bundle_name`. The following json
attribute can be specified:
 * compare: which mode must we use to check the difference, default: digest
 * dest: destination directory
 * exclude_dirs:  exclude this dirs from copying, default: [ ".git", ".svn" ]
 * mog: mode/owner/group  of all files/directories, default not set
 * preserve: preserve same mode as source directory, default: false
 * purge: exact copy of the source directory, default: false
 * run_bundle: The name of the bundle to run when something is repaired, default not set.
 * source: source directory
 * type_check:  Check if destination adn source are of the same type, default: false

The  copy attributes can be overriden or specified  by json data, eg:
```json
"slurm": {
    "copy_dirs": [
        {
            "dest": "$(sara_data.slurm[scripts_dir])",
            "exclude_dirs": [ ".git", ".svn" ],
            "mog": [ "0700", "root", "root" ],
            "purge": "true",
            "source": "cf_bundles_dir/slurm/scripts"
        }
    ]
}
```

## sara_service_copy_files(bundle_name)

This bundle will read the json attribute `copy_files` from the specified `bundle_name`. The following json
attribute can be specified:
 * dest: destination
 * mog: mode/owner/group of the file
 * run_bundle: The name of the bundle to run when something is repaired, default not set.
 * secure_cp from mpf is used to copy the files
 * source: source

The `copy_files` can specified by json data eg:
```json
"slurm": {
    "copy_files": [
        {
            "dest": "$(slurm.config_dir)/job_submit.lua",
            "source": "cf_bundles_dir/slurm/lisa/job_submit.lua",
            "mog": ["0644", "slurm", "slurm"]
        }
    ]
}
```
@endif

body copy_from sara_cp_dir(bundle_name, var, server)
{
    servers     => { "$(server)" };
    compare     => "$($(bundle_name).$(var)[compare])";
    source      => "$($(bundle_name).$(var)[source])";
    purge       => "$($(bundle_name).$(var)[purge])";
    preserve    => "$($(bundle_name).$(var)[preserve])";
    type_check  => "$($(bundle_name).$(var)[type_check])";
}

body copy_from sara_template_local_dcp(from)
{
    compare => "digest";
    source => "$(from)";
    copylink_patterns => { ".*" };
}

bundle agent sara_cp_dir_default
{
    vars:
       any::
            "attributes" data => parsejson('{
                "compare": "digest",
                "preserve": "false",
                "purge": "false",
                "sync": "false",
                "type_check": "false"
                }');
}

@if minimum_version(99.9)

## sara_make_cron_file(name. data)

This bundle install system cronjob in `/etc/cron.d`. The bundle is called with:
 * sara_make_cron_file(name, data)

Where:
 * name: is the name of the cronjob file
 * data: Json data. eg: 2 minute cronjob
```json
{
    "minute": "*/2",
    "hour": "*",
    "day_of_month": "*",
    "month": "*",
    "day_of_week": "*",
    "user": "root",
    "command": "/bin/ls"
}
```
@endif
bundle agent sara_make_cron_file(name, data)
{
    vars:
        any::
            "cron_dir" string => "/etc/cron.d";
            "header" string => "## Maintained by CFengine$(const.n)#$(const.n)";

            "template" string => "$(header){{{minute}}} {{{hour}}} {{{day_of_month}}} {{{month}}} {{{day_of_week}}} {{{user}}} {{{command}}}";

            "job_str" string => string_mustache( "$(template)", "@(data)");

    classes:
        any::
            "CRON_VIXIE" expression => isdir("$(cron_dir)");

    files:
@if minimum_version(3.12)
        CRON_VIXIE::
            "$(cron_dir)/$(name)"
                comment => "Vixie cron: one command per file",
                create => "true",
                edit_template_string   => "$(template)$(const.n)",
                template_method => "inline_mustache",
                template_data => "@(data)",
                classes => results("namespace", "sara_make_cron_file_$(name)");
@endif

    methods:
        CRON_VIXIE.(cfengine_3_10|cfengine_3_7)::
            "" usebundle => file_make("$(cron_dir)/$(name)", "$(job_str)"),
                classes => results("namespace", "sara_make_cron_file_$(name)");

    reports:
        any::
            "'$(name)' is installed in $(cron_dir): $(const.n)$(job_str)"
                ifvarclass => "DEBUG|DEBUG_$(this.bundle)";

        !CRON_VIXIE::
            "No Vixie cron software installed";
}

bundle agent sara_service_copy_dirs(bundle_name)
{
vars:
    any::
        "index" slist => getindices("sara_data.$(bundle_name)[copy_dirs]");

        "cp_attr_$(index)" data => mergedata("sara_cp_dir_default.attributes", "sara_data.$(bundle_name)[copy_dirs][$(index)]"),
            comment => "Merge each definition the copy default attributes";

        "exclude_dir_$(index)" slist => { ".git", ".svn" },
            comment => "Default exclude directories settings for copy_dirs",
            ifvarclass => and(
                not(isvariable("sara_data.$(bundle_name)[copy_dirs][$(index)][exclude_dirs]"))
            );

        "exclude_dir_$(index)" data => mergedata("sara_data.$(bundle_name)[copy_dirs][$(index)][exclude_dirs]"),
            comment => "Exclude directory settings from json data",
            ifvarclass => and(
                isvariable("sara_data.$(bundle_name)[copy_dirs][$(index)][exclude_dirs]")
            );

    cfengine_3_7|cfengine_3_10::
        "debug_str" string => format("%S", "sara_data.$(bundle_name)[copy_dirs]"),
            comment => "Old debug method",
            ifvarclass => "DEBUG|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";

classes:
    any::
        "$(bundle_name)_copy_dirs_$(index)_run_bundle" expression => isvariable("sara_data.$(bundle_name)[copy_dirs][$(index)][run_bundle]"),
            comment => "Must we run a bundle when a directory is changed";

        "$(bundle_name)_copy_dirs_$(index)_mog_class" expression => isvariable("sara_data.$(bundle_name)[copy_dirs][$(index)][mog]"),
            comment => "Must we set directory permission and doit recurse";

files:
    any::
        ## Can not use $(this.bundle) in copy_from body, bug?
        "$(sara_data.$(bundle_name)[copy_dirs][$(index)][dest])"
            comment => "Copy bundle dirs and set class if there is a change",
            copy_from => sara_cp_dir("sara_service_copy_dirs", "cp_attr_$(index)", "$(sys.policy_hub)"),
            depth_search => recurse_ignore("inf", "@(exclude_dir_$(index))" ),
            classes => results("namespace", "$(bundle_name)_copy_dirs_$(index)"),
            ifvarclass => and(
                not(canonify("$(bundle_name)_copy_dirs_$(index)_kept"))
            );

        "$(sara_data.$(bundle_name)[copy_dirs][$(index)][dest])"
            comment => "Set permission on all files/dirs if requested",
            perms => mog(
                "$(sara_data.$(bundle_name)[copy_dirs][$(index)][mog][0])",
                "$(sara_data.$(bundle_name)[copy_dirs][$(index)][mog][1])",
                "$(sara_data.$(bundle_name)[copy_dirs][$(index)][mog][2])"
            ),
            depth_search => recurse_ignore("inf", "$(sara_data.$(bundle_name)[copy_dirs][$(index)][exclude_dirs])" ),
            ifvarclass => and(
                canonify("$(bundle_name)_copy_dirs_$(index)_mog_class")
            );

methods:
    any::
        "" usebundle => "$(sara_data.$(bundle_name)[copy_dirs][$(index)][run_bundle])",
            ifvarclass => and(
                canonify("$(bundle_name)_copy_dirs_$(index)_repaired"),
                canonify("$(bundle_name)_copy_dirs_$(index)_run_bundle")
                );

@if minimum_version(3.11)
        "" usebundle => sara_show_data("sara_data", "$(bundle_name)[copy_dirs][$(index)]"),
            ifvarclass => "DEBGU|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
        "" usebundle => sara_show_data("$(this.bundle)", "exclude_dir_$(index)"),
            ifvarclass => "DEBGU|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
        "" usebundle => sara_show_data("$(this.bundle)", "cp_attr_$(index)"),
            ifvarclass => "DEBGU|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
@endif

reports:
    cfengine_3_7|cfengine_3_10::
        "$(this.bundle):$(bundle_name):$(debug_str)"
            ifvarclass => "DEBUG|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
}

bundle agent sara_service_copy_files(bundle_name)
{
vars:
    any::
        "index" slist => getindices("sara_data.$(bundle_name)[copy_files]");

    cfengine_3_7|cfengine_3_10::
        "debug_str" string => format("%S", "sara_data.$(bundle_name)[copy_files]"),
            comment => "Old debug method",
            ifvarclass => "DEBUG|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";

classes:
    any::
        "$(bundle_name)_copy_files_$(index)_run_bundle" expression => isvariable("sara_data.$(bundle_name)[copy_files][$(index)][run_bundle]"),
            comment => "Must we run a bundle when a file is copied";

files:
    any::
        "$(sara_data.$(bundle_name)[copy_files][$(index)][dest])"
            comment => "Copy bundle files and set class if its get copied",
            copy_from => secure_cp("$(sara_data.$(bundle_name)[copy_files][$(index)][source])", "$(sys.policy_hub)"),
            classes => results("namespace", "$(bundle_name)_copy_files_$(index)");

        "$(sara_data.$(bundle_name)[copy_files][$(index)][dest])"
            comment => "Check permission",
            perms => mog(
                "$(sara_data.$(bundle_name)[copy_files][$(index)][mog][0])",
                "$(sara_data.$(bundle_name)[copy_files][$(index)][mog][1])",
                "$(sara_data.$(bundle_name)[copy_files][$(index)][mog][2])"
            );

methods:
    any::
        "" usebundle => "$(sara_data.$(bundle_name)[copy_files][$(index)][run_bundle])",
            ifvarclass => and(
                canonify("$(bundle_name)_copy_files_$(index)_repaired"),
                canonify("$(bundle_name)_copy_files_$(index)_run_bundle")
                );

@if minimum_version(3.11)
        "" usebundle => sara_show_data("sara_data", "$(bundle_name)[copy_files][$(index)]"),
            ifvarclass => "DEBGU|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
@endif

reports:
    cfengine_3_7|cfengine_3_10::
        "$(this.bundle):$(bundle_name):$(debug_str)"
            ifvarclass => "DEBUG|DEBUG_$(bundle_name)|DEBUG_$(this.bundle)";
}
